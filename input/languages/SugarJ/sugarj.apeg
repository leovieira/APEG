apeg SugarJ;

/**
 This grammar implements the SugarJ languages
 
 Conceitualmente, uma definião de "sugar" tem duas partes: definição da sintaxe e regras de transformação ou desugaring
 Eu irei introduzir somente a parte de sintaxe, pois é o que interessa para a gente.
**/

/**
 This nonterminal defines the syntax of a Sugar 
 
 Notes: The modifier must be public;
**/

functions functions.sugarj.SugarJFunctions;

sugar_declaration[String package]:
  ws modifiers ws 'sugar' ws Id ws '{'  ws defining_syntax ws '}'
  ;
 
modifiers: 
  'public'
  ;
  
/**
 This nonterminal defines the syntax of the rules. We use the PEG style
**/

defining_syntax:
  'context-free syntax' ws peg_rule
  ; 

peg_rule:
  (ws peg_expr ws '->' ws Id ws ';')*
  ;
  
peg_expr:
  peg_seq (ws '/' ws peg_seq)*
  ;
  
peg_seq:
  peg_predicate (ws peg_predicate)* / 
  ;

peg_predicate:
    '!' peg_unary_op
  / '&' peg_unary_op
  / peg_unary_op
  ;

peg_unary_op locals[String s]:
    peg_factor<s> '*'
  / peg_factor<s> '+'
  / peg_factor<s> '?'
  / peg_factor<s>
  ;
  
peg_factor returns[String s]:
    s=(Literal
  / Id
  / '[' RANGE+ ']'
  / '.')
  / '(' peg_expr ')' {s='()';}
  ;
  
Literal:
  '\'' (!'\'' Char)+ '\''
  ;

Char:
    '\\' ('n' / 'r' / 't' / 'b' / 'f' / '"' / '\'' / '\\' )
  /  !'\\' .
  ;
  
Id:
  Letter (Letter / Digit / '_')*
  ;
  
Letter:
  [a-zA-Z]
  ;
  
Digit:
  [0-9]
  ;

RANGE:
    Char '-' Char 
  / !']' Char
  ;
  
/**
 This nonterminal defines white space
**/

ws:
  (space / comment)*;

space:
  ' ' / '\t' / endOfLine;

endOfLine:
  '\r\n' / '\r' / '\n'; 

comment:
    '//' (!endOfLine .)* endOfLine
  / '/*' (!'*/' .)* '*/';